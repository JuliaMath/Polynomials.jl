"""
    ngcd(p, q, [k]; kwargs...)

Find numerical GCD of polynomials `p` and `q`. Refer to [`NGCD.ngcd(p,q)`](@ref) for details.

The main entry point for this function is `gcd(p, q, method=:numerical)`, but `ngcd` outputs the gcd factorization -- `u, v, w` with `u*v ‚âà p` and `u*w ‚âà q` -- along with `Œò`, an estimate on how close `p`,`q` is to a gcd factorization of degree `k` and `Œ∫` the GCD condition number.

In the case `degree(p) ‚â´ degree(q)`,  a heuristic is employed to first call one step of the Euclidean gcd approach, and then call `ngcd` with relaxed tolerances.

"""
function ngcd(p::P, q::Q,
              args...;
              kwargs...) where {T,X,P<:StandardBasisPolynomial{T,X},
                                         S,Y,Q<:StandardBasisPolynomial{S,Y}}
    if (degree(q) > degree(p))
        u,w,v,Œò,Œ∫ =  ngcd(q,p,args...;kwargs...)
        return (u=u,v=v,w=w, Œò=Œò, Œ∫=Œ∫)
    end
    if degree(p) > 5*(1+degree(q))
        a,b = divrem(p,q)
        return ngcd(q, b, args...; Œª=100,  kwargs...)
    end

    # easy cases
    degree(p) < 0  && return (u=q,      v=p, w=one(q),  Œ∏=NaN, Œ∫=NaN)
    degree(p) == 0 && return (u=one(q), v=p, w=q,       Œ∏=NaN, Œ∫=NaN)
    degree(q) < 0  && return (u=one(q), v=p, w=zero(q), Œ∏=NaN, Œ∫=NaN)
    degree(q) == 0 && return (u=one(p), v=p, w=q,       Œò=NaN, Œ∫=NaN)
    p ‚âà q          && return (u=p,v=one(p),  w=one(p),  Œ∏=NaN, Œ∫=NaN)
    Polynomials.assert_same_variable(p,q)

    R = promote_type(float(T), float(S))
    ùë∑ = Polynomials.constructorof(promote_type(P,Q)){R,X}

    ps = R[p·µ¢ for p·µ¢ ‚àà coeffs(p)]
    qs = R[q·µ¢ for q·µ¢ ‚àà coeffs(q)]

    # cancel zeros
    nz = min(findfirst(!iszero, ps), findfirst(!iszero, qs))
    if nz == length(qs)
        x = variable(p)
        u = x^(nz-1)
        v,w = ùë∑(ps[nz:end]), ùë∑(qs[nz:end])
        return (u=u, v=v, w=w, Œò=NaN, Œ∫=NaN)
    end

    ## call ngcd
    p‚Ä≤ = PnPolynomial{R,X}(ps[nz:end])
    q‚Ä≤ = PnPolynomial{R,X}(qs[nz:end])
    out = NGCD.ngcd(p‚Ä≤, q‚Ä≤, args...; kwargs...)

    ## convert to original polynomial type
    ùë∑ = Polynomials.constructorof(promote_type(P,Q)){R,X}
    u,v,w = convert.(ùë∑, (out.u,out.v,out.w))
    if nz > 1
        u *= variable(u)^(nz-1)
    end

    (u = u, v = v, w = w, Œò = out.Œò, Œ∫ = out.Œ∫)

end

"""
    square_free(p)

Use `ngcd` to identify the square-free part of the polynomial `p`.
"""
square_free(p) = ngcd(p, derivative(p)).v

"""
    rank_reveal(A; atol, rtol)

This is the high rank-revealing algorithm of [Lee, Li, and Zeng](http://archive.ymsc.tsinghua.edu.cn/pacm_download/285/8743-RankRev_paper.pdf) DOI: DOI 10.1007/s11075-017-0328-7.
"""
function rank_reveal(A::AbstractMatrix{T}; kwargs...) where {T <: AbstractFloat}
    m, n = size(A)
    @assert m ‚â• n
    q = qr(A)

    R = UpperTriangular(q.R)
    w = Vector{eltype(A)}(undef, n)
    Œª = norm(A, Inf)
    œÑ = norm(R, Inf)
    NGCD.rank_reveal!(R, w, Œª, œÑ; kwargs...)

end



## ---- the work is done in this module

module NGCD
using Polynomials, LinearAlgebra
import Polynomials: PnPolynomial, constructorof

"""
    ngcd(p::PnPolynomial{T,X}, q::PnPolynomial{T,X}, [k::Int];
             atol = eps(real(T))^(5/6),       # residual over Œ†‚Çñ
             rtol = eps(real(T)),
             satol = atol,
             srtol = rtol,
             Œª=one(real(T)),
             scale::Bool=false
         )

Computes numerical GCD of polynomials `p` and `q`.

Returns ``u, v, w, Œò, Œ∫`` where ``u‚ãÖv ‚âà p`` and ``u‚ãÖw ‚âà q`` (polynomial
multiplication); ``Œò`` (`\\Theta[tab]`) is the residual error (``‚Äñ
[u‚ãÖv,u‚ãÖw] - [p,q] ‚Äñ``); and ``Œ∫`` (`\\kappa[tab]`) is the numerical gcd
condition number estimate. When `scale=true`, ``u‚ãÖv ‚âà ps/‚Äñps‚Äñ‚ÇÇ`` and
``u‚ãÖw ‚âà qs/‚Äñqs‚Äñ‚ÇÇ``.

The numerical GCD problem is defined in [1] (5.4). Let ``(p,q)`` be a
polynomial pair with degree ``m``, ``n``. Let ``Œ°‚Çò‚Çô`` be set of all
such pairs. Any given pair of polynomials has an exact greatest common
divisor, ``u``, of degree ``k``, defined up to constant factors. Let
``Œ°·µè‚Çò‚Çô`` be the manifold of all such ``(p,q)`` pairs with exact gcd of
degree ``k``. A given pair ``(p,q)`` with exact gcd of degree ``j``
will have some distance ``Œò·µè`` from ``P·µè``.  For a given threshold
``œµ>0`` a numerical GCD of ``(p,q)`` within ``œµ`` is an exact GCD of a
pair ``(pÃÇ,qÃÇ)`` in ``Œ°·µè`` with

``‚Äñ (p,q) - (pÃÇ,qÃÇ) ‚Äñ ‚â§ Œò·µè``, where ``k`` is the largest value for
which ``Œò·µè < œµ``.

(In the ``œµ ‚Üí 0`` limit, this would be the exact GCD.)


Suppose ``(p,q)`` is an ``œµ`` pertubation from ``(pÃÇ,qÃÇ)`` where ``(pÃÇ,qÃÇ)`` has an exact gcd of degree ``k``, then ``degree(gcd‚Çë(p,q)) = k``; as ``œµ ‚Üí 0``, ``gcd‚Çë(p,q) ‚Üí gcd(pÃÇ, qÃÇ)``, and

``\\limsup_{(p,q)‚Üí(pÃÇ,qÃÇ)} \\inf{ ‚Äñ (u,v,w) - (uÃÇ,vÃÇ,wÃÇ) ‚Äñ} / ‚Äñ (p,q) - (pÃÇ,qÃÇ) ‚Äñ < Œ∫‚Çë(p,q)``.

``Œ∫`` is called the numerical GCD condition number.


The Zeng algorithm proposes a degree for ``u`` and then *if* a triple
``(u,v,w)`` with ``u`` of degree ``k`` and ``(u‚ãÖv, u‚ãÖw)`` in ``Œ°·µè‚Çò‚Çô``
can be found satisfying ``‚Äñ (u‚ãÖv, u‚ãÖw) - (p,q) ‚Äñ < œµ`` then
``(u,v,w)`` is returned; otherwise the proposed degree is reduced and
the process repeats. If not terminated, at degree ``0`` a constant gcd
is returned.

The initial proposed degree is the first ``j``, `j=min(m,n):-1:1`,
where ``S‚±º`` is believed to have a singular value of ``0`` (``S‚±º``
being related to the Sylvester matrix of `p` and `q`). The
verification of the proposed degree is done using a Gauss-Newton
iteration scheme holding the degree of ``u`` constant.

## Scaling:

If `scale=true` the gcd of ``p/‚Äñp‚Äñ‚ÇÇ`` and ``q/‚Äñq‚ÇÇ`` is identified. When the
polynomials have large norms, scaling -- or using a relative tolerance
-- can be necessary to numerically identify the degree of the gcd.

## Tolerances:

There are two places where tolerances are utilized:

* For a given `k`, the algorithm refines values `u,v,w`. The value `Œò·µè` is estimated by the difference between ``(u ‚ãÖ v, u ‚ãÖ w)`` and ``(p,q)``. A tolerance of `œÅ` is used to test if this is smaller than specified. The arguments `atol` and `rtol` are used to compute `œµ=max(atol, (‚Äñ(p,q)‚Äñ‚ÇÇ)*Œ∫*rtol)`

* The value `œµ` is also used to determine if the Sylvester matrix for a given `j`, `S‚±º`, is singular. The theory has ``œµ`` the same a above, but we this implementation uses `œÅ = max(satol, ‚Äñ(p,q)‚Äñ‚ÇÇ*srtol)`, which seems to track the scaling that is needed due to floating point approximations. The theory states that if `Œò·µè < œµ` then `œÉ‚Çã‚ÇÅ < œµ ‚àö(m - j + 1)`.

The default choice for `œµ` works reasonably well for a range of polynomials, but scaling or some other choice of `œµ` is needed for some cases.

## Specified degree:

When `k` is specified, a value for ``(u, v, w)`` is identified with ``degree(u)=k``. No tolerances are utilized in computing ``Œò·µè``.


Output:

The function outputs a named tuple with names (`u`, `v`, `w`, `Œò`, `Œ∫`). The components `u`,`v`,`w` estimate the gcd and give the divisors. The value `Œò` is the residual error and `Œ∫` estimates the numerical condition number.

Example:

```jldoctest ngcd
julia> using Polynomials

julia> x = variable(Polynomial{Float64})
Polynomial(1.0*x)

julia> p = (x+10)*(x^9 + x^8/3 + 1);

julia> q = (x+10)*(x^9 + x^8/7 - 6/7);

julia> degree(gcd(p, q))
0

julia> degree(gcd(p, q, method=:numerical))  # u a degree 1 polynomial
1
```

This example perturbs `q` more than the default tolerance, so `atol` is set. We can see that the residual error found is on the order of `1e-9`:

```jldoctest ngcd
julia> p = (x-1)*(x-2)*(x-3);

julia> q = (x-2)*(x-3)*(x-4) + (1e-8)*x^2;

julia> out = Polynomials.ngcd(p, q, atol=1e-8);

julia> degree(out.u)
2

julia> round(log10(out.Œò))
-9.0

julia> out = Polynomials.ngcd(p, q);

julia> degree(out.u)
0
```


Reference:

[1] The Numerical Greatest Common Divisor of Univariate Polynomials
by Zhonggang Zeng;
[url](http://homepages.neiu.edu/~zzeng/uvgcd.pdf);
[doi](https://doi.org/10.1090/conm/556/11014)

Note: Based on work by Andreas Varga

"""
function ngcd(p::PnPolynomial{T,X},
              q::PnPolynomial{T,X};
              atol = eps(real(T))^(5/6),       # residual over Œò·µè
              rtol = Base.rtoldefault(real(T)),
              satol = atol,                    # singular tolerance
              srtol =  eps(real(T)),
              scale::Bool=false,
              Œª::Real = one(real(T)),          # not used
              min‚±º = -1
              ) where {T, X}

    m,n = length(p)-1, length(q)-1
    (m == 1 || n == 0) && return trivial_gcd(p, q)

    @assert m >= n

    # scale
    np‚ÇÇ, nq‚ÇÇ = norm(p,2), norm(q,2)
    if scale
        p ./= np‚ÇÇ
        q ./= nq‚ÇÇ
        np‚ÇÇ =  nq‚ÇÇ = one(T)
    end
    npq‚ÇÇ = sqrt(np‚ÇÇ^2 + nq‚ÇÇ^2)


    # pre-allocate storage
    Q = zeros(T, m + n, m + n) # for QR decomposition of Sylvester matrices
    R = zeros(T, m + n, m + n)
    uv = copy(p) # storage for u·µ¢ * v·µ¢
    uw = copy(q) # storage for u·µ¢ * w·µ¢
    x = Vector{T}(undef, m + n) # used to find œÉ‚Çã‚ÇÅ

    # j is degree of proposed gcd j ‚â§ n ‚â§ m
    j = n  # We count down Sn, S_{n-1}, ..., S‚ÇÇ, S‚ÇÅ
    S‚Çì = SylvesterMatrix(p, q, j)    # initial Sylvester matrix [C‚Çô‚Çã‚±º‚Çä‚ÇÅ(p), C‚Çò‚Çã‚±º‚Çä‚ÇÅ(q)]

    A0 = zeros(T, m+1, 2) # storage for use with extend_QR!
    A0[:,1] = coeffs(p)
    A0[end-n:end,2] = coeffs(q)

    nr, nc = size(S‚Çì) # m+1, m-n+2
    F = qr(S‚Çì)
    copyto!(view(Q, 1:nr, 1:nr), F.Q)
    R[1:nc, 1:nc] .= F.R

    # tolerances
    atol, satol, rtol, srtol = Œª*atol, Œª*satol, Œª*rtol, Œª*srtol
    œÅ = max(satol, npq‚ÇÇ * srtol)

    while true
        V = UpperTriangular(view(R, 1:nc, 1:nc))
        xx = view(x, 1:nc)
        œÉ‚Çã‚ÇÅ = smallest_singular_value!(xx, V, œÅ *  sqrt(m - j + 1))
        #@show j, œÉ‚Çã‚ÇÅ, œÅ *  sqrt(m - j + 1), npq‚ÇÇ

        # Lemma 7.1: If (p,q) is w/in œµ of P^k_{mn} then œÉ‚Çã‚ÇÅ < œµ‚àö(m-j+1)
        if œÉ‚Çã‚ÇÅ ‚â§ œÅ *  sqrt(m - j + 1)
            # candidate for degree; refine u‚ÇÄ, v‚Çí, w‚ÇÄ to see if œÅ < œµ
            if iszero(œÉ‚Çã‚ÇÅ)
                # determinant is 0
                u, v, w = initial_uvw(Val(:iszero), j, p, q, xx)
            else
                u, v, w = initial_uvw(Val(:ispossible), j, p, q, xx)
            end
            œµ‚Çñ, Œ∫ = refine_uvw!(u, v, w, p, q, uv, uw)
            # we have limsup Œò·µè / ‚Äñ(p,q) - (pÃÉ,qÃÉ)‚Äñ = Œ∫, so
            # ‚ÄñŒò·µè‚Äñ ‚â§ Œ∫ ‚ãÖ ‚Äñ(p,q)‚Äñ ‚ãÖ œµ seems a reasonable heuristic.
            # Too tight a tolerance and the right degree will be missed; too
            # lax, and larger degrees will be accepted. We are using
            # `‚àöeps()` for `rtol`, but that may be too lax and is subject to
            # change.
            œµ = max(atol, npq‚ÇÇ * Œ∫ * rtol)
            #@show œµ‚Çñ, œµ, Œ∫
            if œµ‚Çñ ‚â§ œµ
                #@show :success, œÉ‚Çã‚ÇÅ, œµ‚Çñ
                return (u=u, v=v, w=w, Œò=œµ‚Çñ, Œ∫=Œ∫)
            end
            #@show :failure, j
        end

        # reduce possible degree of u and try again with S‚±º‚Çã‚ÇÅ
        # unless we hit specified minimum, in which case return it
        # min‚±º = -1
        if j == min‚±º
            u, v, w = initial_uvw(Val(:ispossible), j, p, q, xx)
            œµ‚Çñ, Œ∫ = refine_uvw!(u, v ,w, p, q, uv, uw)
            return (u=u, v=v, w=w, Œò=œµ‚Çñ, Œ∫=Œ∫)
        end

        # Try again with a smaller j
        j -= 1
        nr += 1
        nc += 2
        nc > nr && break
        extend_QR!(Q, R, nr, nc, A0) # before Q‚ãÖR = S‚±º, now Q‚ãÖR = S‚±º‚Çã‚ÇÅ
    end

    return trivial_gcd(p, q)

end

# fixed `k`
function ngcd(p::P, q::P, k::Int) where {T, X, P <: PnPolynomial{T,X}}

    m, n = length(p)-1, length(q)-1
    S‚Çì = SylvesterMatrix(p,q,k)
    F = qr(S‚Çì)
    R = UpperTriangular(F.R)
    x = zeros(T, size(S‚Çì, 2))
    np = norm(p)
    œÉ‚Çã‚ÇÅ = smallest_singular_value!(x, R)
    w, v = P(x[1:(n-k+1)]), P(-x[(n-k+2):end])
    u = solve_u(v, w, p, q, k)
    œÅ‚Çñ, Œ∫ = refine_uvw!(u, v, w, p, q, u*v, u*w)
    return (u=u, v=v, w=w, Œò=œÅ‚Çñ, Œ∫=Œ∫)
end

function trivial_gcd(p::P, q) where {T, X, P <: PnPolynomial{T, X}}
    u, v, w = one(P), p, q
    return (u=u, v=v, w=w, Œò=zero(T), Œ∫=NaN)
end


# A = QR solve by iteration for largest eigenvalue of A^-1
# modifies w in place

# https://arxiv.org/abs/2103.04196
# Find smallest singular value
# stop when value is less then Œ∏ or Œî=s‚±º - s‚Çã‚ÇÅ is small
function smallest_singular_value!(w, R::UpperTriangular{T},
                                  Œ∏,
                                  œµ‚Çò = eps(real(T))
                                  ) where {T}

    # Cant' handle singular matrices
    iszero(det(R)) && return zero(T)

    nR‚Çí‚Çí = norm(R, Inf)
    MAXSTEPS = 50
    s‚±º = s‚±º‚Çã‚ÇÅ = typemax(real(T))

    w .= one(T)
    w ./= norm(w)

    j = 1
    while true
        s‚±º‚Çã‚ÇÅ = s‚±º
        s‚±º = smallest_singular_value_one_step!(w, R)

        if s‚±º ‚â§ Œ∏ || abs(s‚±º - s‚±º‚Çã‚ÇÅ) ‚â§ max(1, s‚±º)  * œµ‚Çò *  nR‚Çí‚Çí
            break
        end

        j += 1
        j >= MAXSTEPS && break
    end

    return s‚±º

end

# no tolerance; stop when improvment stops
function smallest_singular_value(A)
    R = UpperTriangular(qr(A).R)
    w = Vector{eltype(R)}(undef, size(R, 2))
    smallest_singular_value!(w, R)
end

function smallest_singular_value!(w, R::UpperTriangular{T}) where {T}
    iszero(det(R)) && return zero(T)

    MAXSTEPS = 50

    s‚±º = typemax(real(T))

    w .= one(T)
    w ./= norm(w)

    j = 1

    wÃÉ = copy(w)
    while true
        s‚Ä≤ = smallest_singular_value_one_step!(wÃÉ, R)

        s‚Ä≤ >  s‚±º && break

        copy!(w, wÃÉ)
        s‚±º = s‚Ä≤
        j += 1
        j > MAXSTEPS && break

    end

    return s‚±º

end


# modify w, return s‚±º after one step
# uses R from QR factorization
function smallest_singular_value_one_step!(w, R)
    ldiv!(R', w)
    w ./= norm(w,2)
    ldiv!(R, w)
    s‚±º = 1/norm(w, 2)
    w .*= s‚±º
    return s‚±º
end


# solve for u from [v,w] \ [p,q]
function solve_u(v::P, w, p, q, j) where {T, X, P<:PnPolynomial{T,X}}
    A = [convmtx(v, j+1); convmtx(w, j+1)]
    b = vcat(coeffs(p), coeffs(q))
    u = A \ b
    return P(u)
end

## Find u‚ÇÄ,v‚ÇÄ,w‚ÇÄ from right singular vector
function initial_uvw(::Val{:ispossible}, j, p::P, q::Q, x) where {T,X,
                                                              P<:PnPolynomial{T,X},
                                                              Q<:PnPolynomial{T,X}}
    # Sk*[w;-v] = 0, so pick out v,w after applying permutation
    m, n = length(p)-1, length(q)-1
    v·µ¢ = vcat(2:m-n+2, m-n+4:2:length(x))
    w·µ¢ = m-n+3 > length(x) ? [1] : vcat(1, (m-n+3):2:length(x))

    v = P(-x[v·µ¢])
    w = P(x[w·µ¢])

    # p194 3.9 C_k(v) u = p or Ck(w) u = q; this uses 10.2
    u = solve_u(v, w, p, q, j)
    return u, v, w

end

# find u‚ÇÄ, v‚ÇÄ. w‚ÇÄ when R is singular.
function initial_uvw(::Val{:iszero}, j, p::P, q::Q, x) where {T,X,
                                                              P<:PnPolynomial{T,X},
                                                              Q<:PnPolynomial{T,X}}

    m,n = length(p)-1, length(q)-1
    S = SylvesterMatrix(p,q,j)
    F = qr(S)
    R = UpperTriangular(F.R)

    if iszero(det(R))
        x .= eigvals(R)[:,1]
    else
        x .= one(T)
        smallest_singular_value!(x, R)
    end

    w = P(x[1:n-j+1]) # ordering of S is not interlaced
    v = P(-x[(n-j+2):end])

    u = solve_u(v,w,p,q,j)
    return u,v,w
end


# extend QR to next size
# Q gets a 1 in nc,nc, 0s should be elswhere
function extend_QR!(Q, R, nr, nc, A0)

    #old Q is m x m, old R is n x n; we add to these
    n = nc - 2
    m = nr - 1
    k,l = size(A0)

    # add two columns to R
    # need to apply Q to top part of new columns
    R[nr-k+1:nr, (nc-1):nc] = A0
    R[1:nr-1, (nc-1):nc] = (view(Q, 1:nr-1, 1:nr-1))' *  R[1:nr-1, (nc-1):nc]

    # extend Q with row and column with identity
    Q[nr,nr] = 1

    # Make R upper triangular using Givens rotations
    for j in nr-1:-1:nc-1
        gj,_ = givens(R[j,nc-1], R[j+1,nc-1], j, j+1)
        rmul!(Q, gj')
        lmul!(gj, R)
    end

    for j in nr-1:-1:nc
        gj,_ = givens(R[j,nc], R[j+1,nc], j, j+1)
        rmul!(Q, gj')
        lmul!(gj, R)
    end

    return nothing

end

## attempt to refine u,v,w
## return residual error, œÅ, estimate for 1/œÉ_2, Œ∫
function refine_uvw!(u::P, v::P, w::P,
                     p, q, uv, uw) where {T,X,
                                          P<:PnPolynomial{T,X}}

    mul!(uv, u, v)
    mul!(uw, u, w)
    œÅ‚ÇÅ = residual_error(p, q, uv, uw)
    iszero(œÅ‚ÇÅ) && return (œÅ‚ÇÅ, NaN)
    # storage
    h, Œ≤ =  u, dot(u,u)  # h = constant * u‚ÇÄ is used
    A = JF(h, u, v, w)
    ŒîfŒ≤ = Fmp(dot(h, u) - Œ≤, p, q, uv, uw)
    Œîz = ones(T, length(u) + length(v) + length(w))
    n = size(A, 2)
    R = UpperTriangular(Matrix{T}(undef, n, n))
    R‚Ä≤ = copy(R)
    uÃÉ, vÃÉ, wÃÉ = copy(u), copy(v), copy(w)

    steps = 0
    #@show steps, œÅ‚ÇÅ
    min·µ¢, Max·µ¢ = 3, 20

    while œÅ‚ÇÅ > 0.0
        steps += 1
        refine_uvw_step!(uÃÉ, vÃÉ, wÃÉ,
                         Œîz, A, ŒîfŒ≤, R)
        mul!(uv, uÃÉ, vÃÉ)
        mul!(uw, uÃÉ, wÃÉ)
        œÅ‚Ä≤ = residual_error(p, q, uv, uw)
        #@show steps, œÅ‚Ä≤
        # don't worry as much about first few,
        # but afterwards each step must be productive
        # terminate when no longer decreasing
        #if steps < min·µ¢ || (steps ‚â§ Max·µ¢ && œÅ‚Ä≤ < 0.95*œÅ‚ÇÅ)
        if œÅ‚Ä≤ < œÅ‚ÇÅ || (steps ‚â§ min·µ¢ && œÅ‚Ä≤ ‚â§ 1.1*œÅ‚ÇÅ)
            œÅ‚ÇÅ = œÅ‚Ä≤
            copy!(R‚Ä≤, R)
            copy!(u.coeffs, uÃÉ.coeffs)
            copy!(v.coeffs, vÃÉ.coeffs)
            copy!(w.coeffs, wÃÉ.coeffs)
            steps ‚â• Max·µ¢ && break
            # update A,b for next iteration
            JF!(A, h, u, v, w)
            Fmp!(ŒîfŒ≤,  dot(h, u) - Œ≤, p, q, uv, uw)
        else
            steps == 1 && copy!(R‚Ä≤, R)
            break
        end
    end
    smallest_singular_value_one_step!(Œîz, R‚Ä≤) # two steps, not one
    œÉ‚ÇÇ = smallest_singular_value_one_step!(Œîz, R‚Ä≤)
    Œ∫ = 1/œÉ‚ÇÇ
    return œÅ‚ÇÅ, Œ∫

end

# update u,v,w, uv, uw
# computes update step of z‚Çñ‚Çä‚ÇÅ = z‚Çñ - Œîz; Œîz = J(z‚Çñ)‚Å∫(f‚Çï(u,v,w) - [Œ≤;p;q])
function refine_uvw_step!(u, v, w,
                          Œîz, J‚Å∫, ŒîfŒ≤, R)

    qrsolve!(Œîz, J‚Å∫, ŒîfŒ≤, R) # Œîz .= J‚Å∫ \ ŒîfŒ≤

    m,n,l = length(u)-1, length(v)-1, length(w)-1

    Œîu·µ¢ = 1:(m+1)
    Œîv·µ¢ = (m+1+1):(m+1 + n+1)
    Œîw·µ¢ = (m + 1 + n + 1 + 1):(m + n + l + 3)

    u .-= view(Œîz, Œîu·µ¢)
    v .-= view(Œîz, Œîv·µ¢)
    w .-= view(Œîz, Œîw·µ¢)

end

## Jacobian of F(u,v,w) = [p, p'] is J(u,v,w)
## [h      0      0;
##  C‚Çñ(v) C‚Çò‚Çã‚Çñ(u) 0;
##  C‚Çñ(w)  0    C‚Çô‚Çã‚Çñ(u)]
function JF(h, u::P, v, w) where {T,X,P<:AbstractPolynomial{T,X}}

    # compute size needed to store
    m, k, j = length(u)-1, length(v)-1, length(w)-1
    n, l = m + k, m + j

    ai, aj = convmtx_size(v, m + 1)
    bi, bj = convmtx_size(u, k + 1)
    di, dj = convmtx_size(w, m + 1)
    fi, fj = convmtx_size(u, j + 1)
    ci, cj = ai, fj
    ei, ej = di, bj

    m, n = 1 + ai + di, aj + bj + cj

    A = zeros(T, m, n)
    JF!(A, h, u, v, w)
    A
end

# compute Jacobian of f‚Çï
function JF!(M, h,  u::P, v, w) where {T,X,P<:AbstractPolynomial{T,X}}

    k = length(u) - 1
    d·µ•, d·µ•·µ• = length(v) - 1, length(w) - 1
    m = k + d·µ•  # degree(p)
    n = k + d·µ•·µ• # degree(q)

    M[1, 1:length(h)] .= h'

    r11, c11 = convmtx_size(v, k+1) # Ck(v) size
    J11 = view(M, 2:(1+r11), 1:c11)
    convmtx!(J11, v, k+1)

    r12, c12 = convmtx_size(u, m-k+1)
    J12 = view(M, 2:(1+r11), (c11+1):(c11+1+c12))
    convmtx!(J12, u, m-k+1)

    r21, c21 = convmtx_size(w, k+1)
    J21 = view(M, (1 + r11 + 1):(1 + r11 + r21), 1:c21)
    convmtx!(J21, w, k+1)

    r23, c23 = convmtx_size(u, n - k+1)
    J23 = view(M, (1 + r11 + 1):(1 + r11 + r23),
               (c21 + c12 + 1):(c21 + c12 + c23))
    convmtx!(J23, u, n-k+1)

    return nothing
end

# create storage for f‚Çï(u,v,w) - [Œ≤; p; q]; fill in
function Fmp(Œî, p::PnPolynomial{T,X}, q, pÃÉ, qÃÉ) where {T,X}
    b = zeros(T, 1 + length(p) + length(q))
    Fmp!(b, Œî, p, q, pÃÉ, qÃÉ)
    b
end

## f‚Çï - [Œ≤; p; q]
## Œî = h‚ãÖu·µ¢ - Œ≤
function Fmp!(b, Œî, p, q, uv, uw)
    b[1] = Œî
    for (i, p·µ¢) ‚àà pairs(p)
        b[2 + i] = uv[i] - p·µ¢
    end
    for (i, q·µ¢) ‚àà pairs(q)
        b[2 + length(p) + i] = uw[i] - q·µ¢
    end
    return nothing
end

# find ||(p,q) - (pÃÉ, qÃÉ)|| treating (,) as vector concatenation of coefficients
function residual_error(p::P, q, pÃÉ, qÃÉ) where {T,X,P<:AbstractPolynomial{T,X}}
    tot = zero(real(T))
    for (p·µ¢, pÃÉ·µ¢) in zip(p, pÃÉ)
        tot += abs2(p·µ¢ - pÃÉ·µ¢)
    end
    for (q·µ¢, qÃÉ·µ¢) in zip(q, qÃÉ)
        tot += abs2(q·µ¢ - qÃÉ·µ¢)
    end
    sqrt(tot)
end

## ---- utils
## ---- QR factorization
function qrsolve!(y::Vector{T}, A, b) where {T <: Float64}
    n = size(A, 2)
    R = UpperTriangular(Matrix{Float64}(undef, n, n))
    qrsolve!(y, A, b, R)
end

function qrsolve!(y::Vector{T}, A, b, R) where {T}
    q = qr(A)
    R .= UpperTriangular(q.R)
    y .= q \ b
end

# Fast least-squares solver for full column rank Hessenberg-like matrices
# By Andreas Varga
function qrsolve!(y::Vector{T}, A, b, R) where {T <: Float64}
    # half the time of ldiv!(y, qr(A), b)
    Base.require_one_based_indexing(A)
    m, n = size(A)
    m < n && error("Column dimension exceeds row dimension")
    _, œÑ = LinearAlgebra.LAPACK.geqrf!(A)
    R .= UpperTriangular(triu(A[1:n,1:n]))

    tran = T <: Complex ? 'C' : 'T'
    LinearAlgebra.LAPACK.ormqr!('L', tran, A, œÑ, view(b,:,1:1))
    R = UpperTriangular(triu(A[1:n,:]))
    ldiv!(y, R, view(b,1:n))
end


"""
    convmtx(v, n::Int)
    convmtx!(M,v,n)
    convmtx_size(v,n)

Convolution matrix.
C = convmtx(v,n) returns the convolution matrix C for a vector v.
If q is a column vector of length n, then C*q is the same as conv(v,q).

"""
function convmtx!(C, v::AbstractVector{T}, n::Int) where T

    #   Form C as the Toeplitz matrix
    #   C = Toeplitz([v; zeros(n-1)],[v[1]; zeros(n-1));  put Toeplitz code inline

    nv = length(v)-1

    @inbounds for j = 1:n
        C[j:j+nv,j] = v
    end

    nothing

end
convmtx_size(v::AbstractVector, n) = (n + length(v) - 1, n)
function convmtx(v::AbstractVector{T}, n::Int) where {T}
    C = zeros(T, convmtx_size(v, n)...)
    convmtx!(C, v, n)
    C
end

# multroot uses vector/matrix interface.
convmtx!(C, v::AbstractPolynomial, n::Int) = convmtx!(C, coeffs(v), n)
convmtx_size(v::AbstractPolynomial, n) = (n + length(v)-1, n)
function convmtx(v::AbstractPolynomial{T}, n::Int) where {T}
    d = length(v)-1
    C = zeros(T, (n + d, n))
    convmtx!(C, v, n)
    C
end

function SylvesterMatrix(p, q, j)
    m, n = length(p)-1, length(q) - 1
    S‚Çì = hcat(convmtx(p, n - j + 1 ),  convmtx(q, m - j + 1))
end

## ----

# non allocating numeric-rank reveal
function rank_reveal!(R::LinearAlgebra.UpperTriangular{T}, w,
                      nA‚Çí‚Çí, nR‚Çí‚Çí;
                      atol = Base.rtoldefault(real(T)),
                      rtol = eps(real(T))) where {T <: AbstractFloat}

    n = size(R, 2)

    d = prod(R[i,i] for i ‚àà 1:n)
    iszero(d) && return(0, zero(T)) # Cant' handle singular matrices


    MAXSTEPS = 50

    Œ∏ = max(atol, nA‚Çí‚Çí * rtol)

    œµ‚Çò = nA‚Çí‚Çí * eps(real(T))
    r = n
    s‚±º = s‚±º‚Çã‚ÇÅ = typemax(real(T))

    for k ‚àà 1:n
        s‚±º = smallest_singular_value!(w, R, Œ∏, œµ‚Çò)

        if s‚±º > Œ∏
            break
        end

        r -= 1

        # W = [w W]
        # can eliminate allocations here!
        # RR = [œÑ*w'; R]
        # for i ‚àà 1:n
        #     G,_ = givens(RR, i, i+1, i)
        #     lmul!(G, RR)
        # end
        # R = UpperTriangular(RR[1:end-1, :])

        œÑ = nR‚Çí‚Çí
        a,b = œÑ*w[1], R[1,1]
        g,d = givens(a, b, 1, 2)
        R[1,1] = d
        for j in 2:n
            a, b =  œÑ*w[j], R[1, j]
            R[1,j] = conj(g.c)*a + conj(g.s)*b
            w[j] = R[2, j]
            R[2,j] = -conj(g.s)*a + conj(g.c)*b
        end

        for i ‚àà 2:(n-1)
            a, b = R[i, i], w[i]
            g, d = givens(a, b, 1, 2)
            R[i,i] = d
            for j ‚àà (i+1):n
                a, b =  R[i,j], w[j]
                R[i,j] = conj(g.c)*a + conj(g.s)*b
                w[j] = R[i+1, j]
                R[i+1, j] = -conj(g.s)*a + conj(g.c)*b
            end
        end

        # n
        g, d = givens(R[n,n], w[n], n-1,n)
        R[n,n] = d

    end
    r, s‚±º
end


end
